%%%-------------------------------------------------------------------
%%% @author mac
%%% @copyright (C) 2016, <COMPANY>
%%% @doc
%%%
%%% @end
%%% Created : 21. Feb 2016 8:49 PM
%%%
%%% This is utility module that produces condor_submit file contents from 3 sources of possible inputs,
%%% in the following order of priority if there is a parameter key conflict:
%%%
%%% 1. Mandatory CF condor parameters: executable, log, error, output
%%% 2. Optional task-level condor parameters, i.e. universe, RequestMemory, RequestCpus
%%% 3. Optional default condor parameters, i.e. universe, should_transfer_files, etc.
%%%
%%% One exception is transfer_input_files: instead of choosing just one of the corresponding
%%% lists, the lists are merged.  This way any custom input files can be specified as well.
%%% Also, input file names are validated to be real files, therefore transfer_input_files param
%%% is the only one expected to be a list of files rather than a complete string and it will
%%% be converted back to comma-separated string once the validation and possibly merging is done
%%%
%%% Mandatory condor parameters that aren't provided in either of the "sources"
%%% will be filled in with VANILLA_DEFAULS
%%%-------------------------------------------------------------------
-module(condor_submit).

-ifdef( TEST ).
-include_lib( "eunit/include/eunit.hrl" ).
-endif.

%% API
-export([
  generate_condor_submit/1,
  generate_condor_submit/2,
  generate_condor_submit/3]).

-define(VANILLA_DEFAULTS,
  #{universe => "vanilla",
    should_transfer_files => "YES",
    run_as_owner => "True",
    when_to_transfer_output => "ON_EXIT"}).

%%
%% This method expects 1 argument
%% 1. Mandatory CF-generated condor parameters, like 'executable', 'log', etc.
%% All the missing necessary parameters like 'universe' will be filled default values
generate_condor_submit(CFParams) -> generate_condor_submit(CFParams, #{}).
%%
%% This method expects 2 arguments:
%% 1. Mandatory CF-generated condor parameters, like 'executable', 'log', etc.
%% 2. declared default condor parameters
%%
generate_condor_submit(#{ executable := Executable,
  log := Log,
  output := Output,
  error := Error,
  initialdir := InitialDir} = CFParams,
    DeclaredParams) when
  is_list(Executable), length(Executable) > 0,
  is_list(Log), length(Log) > 0,
  is_list(Output), length(Output) > 0,
  is_list(Error), length(Error) > 0,
  is_list(InitialDir), length(InitialDir) > 0,
  is_map(DeclaredParams) ->
  CondorParams0  = combine_params(CFParams, DeclaredParams),
  CondorParams = maps:merge(?VANILLA_DEFAULTS, CondorParams0),
  create_submitfile(CondorParams).

%%
%% This method expects 3 arguments:
%% 1. Mandatory CF-generated condor parameters, like 'executable', 'log', etc.
%% 2. declared default condor parameters
%% 3. declared task-level condor parameters (any task-level parameters take precedence over defaults)
%%
generate_condor_submit(CFParams, DeclaredDefaultParams, DeclaredTaskParams)
  when is_map(DeclaredDefaultParams), is_map(DeclaredTaskParams)->
  DefaultParams = combine_params(DeclaredTaskParams, DeclaredDefaultParams),
  generate_condor_submit(CFParams, DefaultParams).


%% INTERNAL methods

validate_input_files( #{ transfer_input_files := [] } = Params) ->
  maps:remove(transfer_input_files, Params);
validate_input_files( #{ transfer_input_files := InputFiles} = Params)
  when is_list(InputFiles) ->
  case lists:all(fun(Elem) -> filelib:is_file(Elem) end, InputFiles) of
    true -> Params;
    false -> {error, io_lib:format("Invalid input file(s) in ~p!", [InputFiles])}
  end;
validate_input_files(#{} = Params) -> Params.


%%% This method will combine parameters from two maps in such a a way that
%% Params1 will take precedence over Params2, but if there is tranfer_input_files parameter
%% present in both it will merge the list of input files into one
combine_params( #{ transfer_input_files := InputFiles1 } = Params1,
                #{ transfer_input_files := InputFiles2 } = Params2 )
  when is_list(InputFiles1), is_list(InputFiles2) ->
  InputFiles = InputFiles1 ++ InputFiles2,
  combine_params(
    maps:put(transfer_input_files, InputFiles, Params1),
    maps:remove(transfer_input_files, Params2)
  );
combine_params(Params1, Params2) when is_map(Params1), is_map(Params2) ->
  maps:merge(validate_input_files(Params2), validate_input_files(Params1)).


%%% The only parameter that might come here as a list instead of complete cs-string
%%% is transfer_input_files because it might be generated by cuneiform and it needs file validation
%%% Therefore any decalred transfer_input_files params need to be first parsed from cs-string
%%% into a list, and once they are validated and possibly merged, they are converted back to cs-string here
input_files_to_cs_string(#{transfer_input_files := InputFiles} = CondorParams) ->
  InputFilesCS = string:join(InputFiles, ", "),
  maps:put(transfer_input_files, InputFilesCS, CondorParams);
input_files_to_cs_string(CondorParams) -> CondorParams.

%%% Just take the param map and output into a string having 'key = value' format
%%% The order of key-value pairs in condor config file doesn't matter at all (I tested)
%%% The resulting string is a complete valid condor submit file contents --
%%% leaving it to cre_htcondor module to decide the name of the file to write them to
create_submitfile(CondorParams0) ->
  CondorParams1 = input_files_to_cs_string(CondorParams0),
  LineSep = io_lib:nl(),
  SubmitFileStr = maps:fold(fun(K, V, Acc) -> [Acc, io_lib:format("~p = ~s", [K,V]), LineSep] end, "", CondorParams1),
  iolist_to_binary([LineSep, lists:flatten(SubmitFileStr), LineSep, "Queue", LineSep]).


-ifdef( TEST ).

-define (GOOD_CF_PARAMS,
  #{ executable => "do_something.sh",
    log => "job123.log",
    output => "job123.stdout",
    error => "job123.stderr",
    initialdir => ".",
    transfer_input_files => []}).

%%% For testing with condor create `do_something.sh` in the same dir where you run
%%% `condor_submit /tmp/cf_params_test
cf_params_test() ->
  CondorSubmitStr = generate_condor_submit(?GOOD_CF_PARAMS),
  file:write_file("/tmp/cf_params_test", [CondorSubmitStr]).
%%   condor_submit("/tmp/cf_params_test").

%%% If files at the end aren't deleted it produces a valid submitfile: /tmp/default_parameters_test
%%% If run with `condor_submit /tmp/default_parameters_test`
%%% Gives a `WARNING: the line 'docker_image = some_image:latest' was unused by condor_submit. Is it a typo?`
%%% Demonstrating that if some params or their combo are invalid condor will handle the errors/warnings
%%% So cuneiform doesn't have to yet it retains full power of condor submit
default_params_test() ->
  DefaultParams = #{ universe => "docker",
    docker_image => "some_image:latest",
    transfer_input_files => ["/tmp/d1.tmp", "/tmp/d2.tmp"],
    should_transfer_files => "NO" },

  CFParams = maps:put(transfer_input_files, ["/tmp/cf1.tmp", "/tmp/cf2.tmp", "/tmp/cf3.tmp"], ?GOOD_CF_PARAMS),

%%   generate_condor_submit(CFParams, DefaultParams),

  Files = maps:get(transfer_input_files, DefaultParams) ++ maps:get(transfer_input_files, CFParams),
  create_temp_files(Files),
  CondorSubmitStr = generate_condor_submit(CFParams, DefaultParams),
  file:write_file("/tmp/default_params_test", CondorSubmitStr).
%%   condor_submit("/tmp/default_params_test").

% task_params_test() ->
%   DefaultParams = #{ universe => "docker",
%     docker_image => "some_image:latest",
%     transfer_input_files => ["/tmp/d1.tmp", "/tmp/d2.tmp"],
%     should_transfer_files => "NO" },

%   TaskParams = #{ requestMemory => "256M",
%     transfer_input_files => ["/tmp/t1.tmp", "/tmp/t2.tmp"]},

%   CFParams = maps:put(transfer_input_files, ["/tmp/cf1.tmp", "/tmp/cf2.tmp", "/tmp/cf3.tmp"], ?GOOD_CF_PARAMS),

% %%   generate_condor_submit(CFParams, DefaultParams, TaskParams),

%   Files = maps:get(transfer_input_files, DefaultParams) ++
%     maps:get(transfer_input_files, TaskParams) ++
%     maps:get(transfer_input_files, CFParams),

%   CondorSubmitStr = generate_condor_submit(CFParams, DefaultParams, TaskParams),
%   file:write_file("/tmp/task_params_test_error", CondorSubmitStr),

%   create_temp_files(Files),
%   CondorSubmitStr = generate_condor_submit(CFParams, DefaultParams, TaskParams),
%   file:write_file("/tmp/task_params_test", CondorSubmitStr).
%%   condor_submit("/tmp/task_params_test").

create_temp_files(Files) when is_list(Files) ->
  lists:foreach(fun(F) -> file:write_file(F, "") end, Files).

%%% TODO: move out of here into CT, IMPROVE TEST COVERAGE, and uncomment condor_submits

%% DELETE to test with condor
%% delete_temp_files(Files) ->
%%   lists:foreach(fun(F) -> file:delete(F) end, Files).

%% condor_submit(CondorSubmitFile) ->
%%   Out = os:cmd(["condor_submit", CondorSubmitFile]),
%%   OutFile = io_lib:format("/tmp/~s_submit_result", [CondorSubmitFile]),
%%   file:write_file(OutFile, Out).

-endif.



















